plugins {
  id "com.moowork.node" version "1.0.1"
}

node {
  version = '6.9.4'
  npmVersion = '3.10.10'
  download = true
}

task clean(type: Delete) {
  description 'Cleans the front-end.'
  delete 'dist'
  delete 'coverage'
}

task test(type: NpmTask, dependsOn: npmInstall) {
  description 'Tests the front-end.'
  args = ['run', 'testprod']
  inputs.files(fileTree('src'))
  outputs.dir('coverage')
}

task lint(type: NpmTask, dependsOn: [npmInstall]) {
  description 'Runs tslint on all .ts files'
  args = ['run', 'lint']
  inputs.files(fileTree('src').include('**/*.ts'))
}

task version(type: NpmTask, dependsOn: [npmInstall]) {
  description 'Sets the version in package.json'
  args = ['version', project.version]
  ignoreExitValue = true
}

task build(type: NpmTask, dependsOn: [npmInstall, test, lint, 'version']) {
  description 'Builds the front-end.'
  args = ['run', 'buildprod']
  inputs.files(fileTree('src'))
  outputs.dir('dist/prod')
}

