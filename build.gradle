buildscript {
    ext {
        springBootVersion = '1.4.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

jar {
    baseName = 'jmoney'
    version = '0.1.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "passed", "skipped", "failed"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-rest') {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-hibernate5")
    compile("org.apache.commons:commons-lang3:3.4")
    runtime('com.h2database:h2')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task cleanFE(dependsOn: 'front-end:ngClean') {
    description 'Cleans the front-end.'
}
task cleanAll() {
    description 'Cleans both the front-end and back-end.'
}
cleanAll.dependsOn cleanFE
cleanAll.dependsOn clean

task testFE(dependsOn: 'front-end:ngTest') {
    description 'Tests the front-end.'
}
task testAll() {
    description 'Tests both the front-end and back-end.'
}
testAll.dependsOn testFE
testAll.dependsOn test

task buildFE(type: Copy, dependsOn: 'front-end:ngBuild') {
    description 'Builds the front-end.'
    from 'front-end/dist'
    exclude 'out-tsc'
    into 'build/resources/main/static'
}
task buildAll() {
    description 'Builds both the front-end and back-end.'
}
buildAll.dependsOn buildFE
buildAll.dependsOn testFE
buildAll.dependsOn build
build.mustRunAfter buildFE

bootRun.dependsOn buildFE
